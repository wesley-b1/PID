#include <Arduino.h>
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>

#define LOGI(tag, msg) Serial.printf("[INFO][%s] %s\n", tag, msg)
#define LOGW(tag, msg) Serial.printf("[WARN][%s] %s\n", tag, msg)
#define LOGE(tag, msg) Serial.printf("[ERROR][%s] %s\n", tag, msg)

// DEFINIÇÕES P/ AMOSTRAGEM E INÍCIO DO PID
static const TickType_t samplingInterval = 100 / portTICK_PERIOD_MS;
static const TickType_t timeToStartInterval = 5000 / portTICK_PERIOD_MS;
static const uint16_t RESOLUTION = 4096;
// TASKS
TaskHandle_t serialTaskHandle = NULL;
TaskHandle_t iniciarTimersTaskHandle = NULL;
// TIMERS HANDLE
static TimerHandle_t muxConfigTimer = NULL;        // Seleciona planta via multiplexador
static TimerHandle_t getSensorReadingTimer = NULL; // Executa computeU() em malha fechada
static TimerHandle_t stepInputStartTimer = NULL;   // Aplica degrau em malha aberta

// True malha fechada e false para malha aberta
static const bool usarPID = true; // true para (PID) ou false (Degrau)

// Pinos do MUX (CD4052)
static const uint8_t A_PIN = 14;
static const uint8_t B_PIN = 27;

//********************************************************************************************************
// VARIÁVEIS GLOBAIS
volatile float VCC = 3.3;
volatile uint16_t sensorReadingInt;
volatile float sensorReadingVoltage;
// Entradas analógicas
static const uint8_t SENSOR_X = 35; // plantas 1 a 4
static const uint8_t SENSOR_Y = 34; // plantas 5 e 6
// Saídas para atuar a planta
static const uint8_t MV_PIN_1 = 25;
static const uint8_t MV_PIN_2 = 26;
// variáveis de controle
bool iniciarExperimento = false;
bool experimentoIniciado = false;
// VARIÁVEIS PID
unsigned long lastTime = 0;
double u = 0, y = 0;
double sp = 0.75 * RESOLUTION; //  0.75 * 4096 = 3072
double iTerm = 0, lastY = 0;
double kp = 0.0, ki = 0.0, kd = 0.0; // inicialização
double uMin = 0, uMax = 255;
unsigned long sampleTime = samplingInterval * portTICK_PERIOD_MS;
// Seletor de planta (1 a 6)
// 1° ordem [1 ao 4]
// 2° ordem [5 ao 6]
int plantaSelecionada = 4;
//********************************************************************************************************

//=========================================================================================================
// FUNÇÕES PID
void computeU();
void setTunings(double kP, double kI, double kD);
void setControlLimits(double min, double max);
// CALLBACKS
void getSensorReadingCallback(TimerHandle_t xTimer);
void setStepInputReadingCallback(TimerHandle_t xTimer);
void setMUXCallback(TimerHandle_t xTimer);
// CONFIGURAÇÕES
void configurarPinos();
void configurarMUX();
void configurarMalhaFechada();
void configurarMalhaAberta();
// Parar todos os timers e resetar sistema:
void pararTimers();
// Task para ler a serial:
void serialTask(void *pvParameters);
// Função para iniciar os timers quando autorizado:
void iniciarTimersSeNecessario(void *pvParameters);
//=========================================================================================================

void setup()
{
  configurarPinos();
  Serial.begin(115200);
  Serial.println("Digite 'start' para iniciar o experimento.");
  // Criação das tasks
  xTaskCreate(serialTask, "serialTask", 4096, NULL, 1, &serialTaskHandle);
  // iniciarTimerSeNecessario precisa ser NULL na serialTask
  // xTaskCreate(iniciarTimersSeNecessario, "iniciarTimersTask", 4096, NULL, 1, &iniciarTimersTaskHandle);
}

void loop()
{
  vTaskSuspend(NULL); // Tudo controlado por timers
}

// FUNÇÕES PID
// Limita iTerm e u entre uMin e uMax
// Atualiza lastY e lastTime após cada cálculo
void computeU()
{
  unsigned long now = millis();
  int timeChange = (now - lastTime);
  if (timeChange >= sampleTime)
  {
    double e = sp - y; // erro
    iTerm += (ki * e); // termo integral
    if (iTerm > uMax)
      iTerm = uMax;
    else if (iTerm < uMin)
      iTerm = uMin;
    double dY = (y - lastY);      // derivada da PV (não do erro)
    u = kp * e + iTerm - kd * dY; // PID completo
    if (u > uMax)
      u = uMax;
    else if (u < uMin)
      u = uMin;
    lastY = y;
    lastTime = now;
  }
}
// Define os ganhos do PID levando em conta o tempo de amostragem
void setTunings(double kP, double kI, double kD)
{
  double sampleTimeInSec = ((double)sampleTime / 1000);
  kp = kP;
  ki = kI * sampleTimeInSec;
  kd = kD / sampleTimeInSec;
}
// Define os limites da saída u e do termo integral iTerm.
void setControlLimits(double min, double max)
{
  if (min > max)
    return;
  uMin = min;
  uMax = max;
  if (u > uMax)
    u = uMax;
  else if (u < uMin)
    u = uMin;
  if (iTerm > uMax)
    iTerm = uMax;
  else if (iTerm < uMin)
    iTerm = uMin;
}

//***********************************EXECUÇÃO DO PID**************************************************** */
// CALLBACKS
void getSensorReadingCallback(TimerHandle_t xTimer)
{
  // LOGI("ControlePID", "Entrou no callback do sensor");
  //  Serial.println("Entrou no callback do sensor"); // console sem console
  if (plantaSelecionada >= 1 && plantaSelecionada <= 4)
    sensorReadingInt = analogRead(SENSOR_X);
  else if (plantaSelecionada == 5 || plantaSelecionada == 6)
    sensorReadingInt = analogRead(SENSOR_Y);

  y = sensorReadingInt;
  sensorReadingVoltage = (VCC * y) / RESOLUTION;

  if (usarPID)
  {
    computeU();
    if (plantaSelecionada >= 1 && plantaSelecionada <= 4)
    {
      // valor digital máximo aceito pelo DAC é de 2^8 – 1 = 255
      // significa que haverá 255 níveis entre o 0V e a tensão de alimentação na saída do DAC
      // resolução de 8 bits
      dacWrite(MV_PIN_1, (int)u); // enviando U pelo DAC1 GPIO25
    }
    else if (plantaSelecionada == 5 || plantaSelecionada == 6)
    {
      dacWrite(MV_PIN_2, (int)u); // enviando U pelo DAC2 GPIO26
    }
  }

  Serial.print(xTaskGetTickCount() / 1000.0, 1);
  Serial.print(",");
  Serial.print("P");
  Serial.print(plantaSelecionada);
  Serial.print(",");
  Serial.print(sp * VCC / RESOLUTION);
  Serial.print(",");
  Serial.println(sensorReadingVoltage, 3);
}
//**************************************************************************************************** */

void setStepInputReadingCallback(TimerHandle_t xTimer)
{
  if (!usarPID)
  {
    if (plantaSelecionada >= 1 && plantaSelecionada <= 4)
      dacWrite(MV_PIN_1, 255);
    else if (plantaSelecionada >= 5 && plantaSelecionada <= 6)
      dacWrite(MV_PIN_2, 255);
  }
}

void setMUXCallback(TimerHandle_t xTimer)
{
  switch (plantaSelecionada)
  {
  case 1: // Planta 1 -> X0
    // A=0 B=0
    digitalWrite(A_PIN, LOW);
    digitalWrite(B_PIN, LOW);
    break;
  case 2: // Planta 2 -> X1
    // A=1 B=0
    digitalWrite(A_PIN, HIGH);
    digitalWrite(B_PIN, LOW);
    break;
  case 3: // Planta 3 -> X2
    // A=0 B=1
    digitalWrite(A_PIN, LOW);
    digitalWrite(B_PIN, HIGH);
    break;
  case 4: // Planta 4 -> X3
    // A=1 B=1
    digitalWrite(A_PIN, HIGH);
    digitalWrite(B_PIN, HIGH);
    break;
  case 5: // Planta 5 -> Y0
    // A=0 B=0
    digitalWrite(A_PIN, LOW);
    digitalWrite(B_PIN, LOW);
    break;
  case 6: // Planta 6 -> Y1
    // A=1 B=0
    digitalWrite(A_PIN, HIGH);
    digitalWrite(B_PIN, LOW);
    break;
  default:
    // Serial.println("Planta inválida.");
    LOGE("ControlePID", "Planta inválida.");
    return;
  }
  // LOGI("ControlePID", "MUX configurado para Planta");
  Serial.print("MUX configurado para Planta ");
  Serial.println(plantaSelecionada);
}

void configurarPinos()
{
  Serial.begin(115200);
  pinMode(MV_PIN_1, OUTPUT);
  pinMode(MV_PIN_2, OUTPUT);
  pinMode(SENSOR_X, INPUT);
  pinMode(SENSOR_Y, INPUT);
  pinMode(A_PIN, OUTPUT);
  pinMode(B_PIN, OUTPUT);
}

void configurarMUX()
{
  // Timer do MUX (necessário em MA e MF)
  muxConfigTimer = xTimerCreate("muxConfigTimer", pdMS_TO_TICKS(100), pdFALSE, NULL, setMUXCallback);
  xTimerStart(muxConfigTimer, 0);
}

void configurarMalhaFechada()
{
  setControlLimits(0, 255);
  switch (plantaSelecionada)
  {
  case 1:
    // Planta 1 1° ordem
    setTunings(1.922, 0.602, 0.0); // KP, KI, KD
    break;
  case 2:
    // Planta 2 1° ordem
    setTunings(2.033, 0.525, 0.0); // KP, KI, KD
    break;
  case 3:
    // Planta 3 1° ordem
    setTunings(2.054, 0.299, 0.0); // KP, KI, KD
    break;
  case 4:
    // Planta 4 1° ordem
    setTunings(1.961, 0.241, 0.0); // KP, KI, KD
    break;
  }
  dacWrite(MV_PIN_1, 0);
  dacWrite(MV_PIN_2, 0);
  vTaskDelay(pdMS_TO_TICKS(8000)); // Esperar descarga do capacitor por 8s
  // criando timer de malha fechada (com PID ativo)
  getSensorReadingTimer = xTimerCreate("getSensorReadingTimer", samplingInterval, pdTRUE, NULL, getSensorReadingCallback);
  // Confirmando se o getSensorReadingTimer foi criado corretamente
  if (getSensorReadingTimer != NULL)
  {
    LOGI("ControlePID", "Iniciando getSensorReadingTimer!.");
    xTimerStart(getSensorReadingTimer, 0); // inicia o timer
  }
  else
  {
    // Serial.println("Erro ao criar getSensorReadingTimer!");
    LOGE("ControlePID", "Erro ao criar getSensorReadingTimer!.");
  }
}

void configurarMalhaAberta()
{
  // ativando time de  malha aberta (sem PID)
  stepInputStartTimer = xTimerCreate("stepInputStartTimer", timeToStartInterval, pdFALSE, NULL, setStepInputReadingCallback);
  xTimerStart(stepInputStartTimer, 0);
}

// Função para iniciar os timers quando autorizado:
void iniciarTimersSeNecessario(void *pvParameters) // Task de execução única
{
  // while (true)
  //{
  if (iniciarExperimento && !experimentoIniciado)
  {
    configurarMUX();
    if (usarPID && plantaSelecionada >= 1 && plantaSelecionada <= 4)
      configurarMalhaFechada();
    else if (!usarPID && plantaSelecionada >= 5 && plantaSelecionada <= 6)
      configurarMalhaAberta();
    experimentoIniciado = true;
    // Serial.println("Timers iniciados...");
    LOGI("ControlePID", "Timers iniciados....");

    experimentoIniciado = true;
    /*--------------------------------------------------------------------------------*/
    TickType_t tempoInicio = xTaskGetTickCount();
    Serial.print("Timers iniciados aos ");
    Serial.print(tempoInicio / 1000.0, 2);
    Serial.println(" segundos desde o boot.");
    /*--------------------------------------------------------------------------------- */
  }
  iniciarTimersTaskHandle = NULL;
  vTaskDelete(NULL); // autodelete da própria task
  // vTaskDelay(pdMS_TO_TICKS(100));
  // }
}

// parar todos os timers e resetar sistema:
void pararTimers()
{
  // Parando (xTimerStop) e deletando (xTimerDelete) os timers após o STOP
  // Serial.println("Parando timers...");
  LOGI("ControlePID", "Parando timers...");
  if (getSensorReadingTimer != NULL)
  {
    Serial.println("Parando getSensorReadingTimer...");
    xTimerStop(getSensorReadingTimer, 0);   // pausa a contagem
    xTimerDelete(getSensorReadingTimer, 0); // remove o timer da memória
    getSensorReadingTimer = NULL;
  }

  if (stepInputStartTimer != NULL)
  {
    Serial.println("Parando stepInputStartTimer...");
    xTimerStop(stepInputStartTimer, 0);   // pausa a contagem
    xTimerDelete(stepInputStartTimer, 0); // remove o timer da memória
    stepInputStartTimer = NULL;
  }

  if (muxConfigTimer != NULL)
  {
    Serial.println("Parando muxConfigTimer...");
    xTimerStop(muxConfigTimer, 0);   // pausa a contagem
    xTimerDelete(muxConfigTimer, 0); // remove o timer da memória
    muxConfigTimer = NULL;
  }
  dacWrite(MV_PIN_1, 0);
  dacWrite(MV_PIN_2, 0);

  iniciarExperimento = false;
  experimentoIniciado = false;

  if (iniciarTimersTaskHandle != NULL)
  {
    vTaskDelete(iniciarTimersTaskHandle); // remove o timer da memória
    iniciarTimersTaskHandle = NULL;
  }
  Serial.println("Experimento parado. Aguardando novo 'start'.");
}

void serialTask(void *pvParameters)
{
  while (true)
  {
    if (Serial.available())
    {
      String comando = Serial.readStringUntil('\n');
      comando.trim();
      comando.toLowerCase();

      if (comando == "start" && !experimentoIniciado && iniciarTimersTaskHandle == NULL)
      {
        Serial.println("Iniciando experimento...");
        iniciarExperimento = true;
        xTaskCreate(iniciarTimersSeNecessario, "iniciarTimersTask", 4096, NULL, 1, &iniciarTimersTaskHandle);
      }
      else if (comando == "stop")
      {
        Serial.println("Parando experimento...");
        pararTimers(); // isso agora também precisa deletar a task
      }
    }
    vTaskDelay(pdMS_TO_TICKS(50));
  }
}
